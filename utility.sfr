( Constants )
:m pi 3.14159 ;
:m tau pi 2 * ;
:m eps 0.00001 ;
:m inf 10000000 ;
:m _e 2.71828 ;

( Comparators )
:m amin ( arr f )
	arr \{ ( $a $b )
		a *f b *f min =>m
		a b a *f m == select
	}
;
:m amax ( arr f )
	arr \{ ( $a $b )
		a *f b *f max =>m
		a b a *f m == select
	}
;
:m minmax ( $a $b ) [ a b min a b max ] ;
: smin-power ( a:float b:float k:float -> float )
	a k ** =a
    b k ** =b

    	a b * a b + /
    	1 k /
    **
;
: smin-poly ( a:float b:float k:float -> float )
	.5 .5 b a - * k / + 0 1 clamp =h
		b a h mix
		[ k h 1 h - ] \*
	-
;
: smin-exp ( a:float b:float k:float -> float )
    k neg a * exp k neg b * exp + log neg k /
;

:m geq ( a x )
	a x step
;
:m eq ( a x )
	1 a x - sign abs -
;

( Maths )
: closest-point-line ( a:vec2 b:vec2 point:vec2 -> vec2 )
	point a - =>pa
	b a - =>ba
	pa ba dot ba ba dot / 0 1 clamp =h
	ba h * a +
;
: point-distance-line ( a:vec2 b:vec2 point:vec2 -> float )
	point a b point closest-point-line - length
;
:m deg->rad pi 180 / * ;
:m rad->deg pi 180 / / ;
: rotate-2d ( c:vec2 a:float -> vec2 )
	a cos =ca
	a sin =sa

	[
		c .x ca * c .y sa * -
		c .y ca * c .x sa * +
	]
;
:m rotate-deg ( c a ) c a deg->rad rotate-2d ;
:m pow ** ;
:m sq 2 ** ;
:m round .5 + floor ;

( Coordinate System Utilities )

: cart->polar ( p:vec2 -> vec2 ) [ p .y.x atan2 p length ] ;
: cart->logpolar ( p:vec2 -> vec2 ) [ p .y.x atan2 p length log ] ;
: polar->cart ( p:vec2 -> vec2 ) [ p .x cos p .x sin ] p .y * ;
: logpolar->cart ( p:vec2 -> vec2 ) [ p .x cos p .x sin ] _e p .y ** * ;
: polar-norm ( p:vec2 -> vec2 ) [ p .x tau + tau mod p .y ] ;

:m p+ ( p v ) p cart->polar v + polar->cart ;
:m p* ( p v ) p cart->polar v * polar->cart ;

:m frag->position ( resolution ) gl_FragCoord .xy resolution .xy / 2 * 1 - [ resolution .x.y / 1 ] * ;

( Distance Field Utilities )
:m gradient ( f p )
	[ eps 0 ] =h
	p *f =>v
	[
		p h + *f  p h - *f -
		p h .yx + *f  p h .yx - *f -
	] 2 / h .x / length =>g
	v g abs /
;
:m gradient-vec ( df vf p )
	[ eps 0 ] =h
	p *df =v
	[
		p h + *df *vf  p h - *df *vf -
		p h .yx + *df *vf  p h .yx - *df *vf -
	] 2 / h .x / length =>g
	v *vf g abs / v
;

( Color Operations )
: hsv->rgb ( hsv:vec3 -> vec3 )
    hsv .x 60 / [ 0 4 2 ] + 6 mod 3 - abs 1 - 0 1 clamp =>rgb
    [ 1 1 1 ] rgb hsv .y mix hsv .z *
;

: hsv1->rgb ( hsv:vec3 -> vec3 )
    hsv .x 6 * [ 0 4 2 ] + 6 mod 3 - abs 1 - 0 1 clamp =>rgb
    [ 1 1 1 ] rgb hsv .y mix hsv .z *
;

: rgb->hsv1 ( rgb:vec3 -> vec3 )
	[ 0 -1 3 / 2 3 / -1 ] =K
	[ rgb .bg K .wz ] [ rgb .gb K .xy ] rgb .g.b < select =p
	[ p .xyw rgb .r ] [ rgb .r p .yzx ] rgb .r p .x < select =q
	q .x.w.y min - =d
	[ q .z.w.y - 6 d * eps + / + abs d q .x eps + / q .x ]
;

:m ]rgb ] 255 / ;

:m ->fragcolor ( v ) [ v 1 ] =gl_FragColor ;

( Array Operations )
:m multi2 ( arr )
	[ arr /{ ( x ) arr /{ ( y ) [ x y ] } flatten } flatten ]
;
:m multi3 ( arr )
	[ arr /{ ( x ) arr /{ ( y ) arr /{ ( z ) [ x y z ] } flatten } flatten } flatten ]
;
:m average _ \+ _ size / ;

( Functional Operations )
:m compose ( f g ) { *f *g } ;
:m . \compose ;

( Image Operations )
:m blur-range $[-.05:+.05:.01] ?{ 0 != } ;
:m blur ( f ) blur-range multi2 /f \+ blur-range size sq / ;
